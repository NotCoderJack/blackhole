buildscript {
    repositories {
        for (mavenRepositoryUrl in project.rootProject.ext.mavenRepositoryUrlList) {
            maven { url(mavenRepositoryUrl) }
        }
    }
}
plugins {
    id "org.inferred.processors" version "3.3.0"
    id "org.springframework.boot" version "2.2.2.RELEASE"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "java"
}

group = project.rootProject.ext.constructGroupName("application", project.getName())
version = project.rootProject.constructVersion()
sourceCompatibility = project.rootProject.ext.javaVersion
targetCompatibility = project.rootProject.ext.javaVersion

repositories.clear()
repositories.addAll(buildscript.repositories)

dependencies {
    implementation(project(":lib:core"))
    implementation(project(":lib:render"))
    implementation(project(":lib:ssh"))
    processor(project.rootProject.ext.jarDependency("org.inferred", "freebuilder"))
    implementation(project.rootProject.ext.jarDependency("com.google.guava", "guava"))
    implementation(project.rootProject.ext.jarDependency("org.apache.commons", "commons-lang3"))
    implementation(project.rootProject.ext.jarDependency("org.freemarker", "freemarker"))
    implementation(project.rootProject.ext.jarDependency("org.apache.commons", "commons-lang3"))
    implementation(project.rootProject.ext.jarDependency("commons-io", "commons-io"))
    testImplementation(project.rootProject.ext.jarDependency("org.junit.jupiter", "junit-jupiter-api"))
    testRuntimeOnly(project.rootProject.ext.jarDependency("org.junit.jupiter", "junit-jupiter-engine"))
    implementation("org.springframework.boot:spring-boot-starter-web")
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
}

test {
    useJUnitPlatform()
    systemProperty "blackhole.test.master_host", project.getProperties().getOrDefault("blackhole.test.master_host", null)
    systemProperty "blackhole.test.master_port", project.getProperties().getOrDefault("blackhole.test.master_port", 22)
    systemProperty "blackhole.test.worker_host", project.getProperties().getOrDefault("blackhole.test.worker_host", null)
    systemProperty "blackhole.test.worker_port", project.getProperties().getOrDefault("blackhole.test.worker_port", 22)
    systemProperty "blackhole.test.username", project.getProperties().getOrDefault("blackhole.test.username", "root")
    systemProperty "blackhole.test.password", project.getProperties().getOrDefault("blackhole.test.password", "123456")
}

task runJar(type: Exec) {
    executable("java")
    args(
            String.format("-Dspring.config.location=%s", project.file("application.yaml")),
            "-jar", project.file(String.format("build/libs/%s-%s.jar", project.getName(), project.getVersion())),
            project.file("template"),
    )
    doFirst {
        println(String.format("running command: %s", String.join(" ", getCommandLine())))
    }
    dependsOn(bootJar)
}