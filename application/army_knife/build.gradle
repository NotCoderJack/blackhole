buildscript {
    repositories {
        for (mavenRepositoryUrl in project.rootProject.ext.mavenRepositoryUrlList) {
            maven { url(mavenRepositoryUrl) }
        }
    }
}
plugins {
    id "org.inferred.processors" version "3.3.0"
    id "org.springframework.boot" version "2.2.2.RELEASE"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "java"
}

group = project.rootProject.ext.constructGroupName("application", project.getName())
version = project.rootProject.constructVersion()
sourceCompatibility = project.rootProject.ext.javaVersion
targetCompatibility = project.rootProject.ext.javaVersion

repositories.clear()
repositories.addAll(buildscript.repositories)

dependencies {
    implementation(project(":lib:core"))
    implementation(project(":lib:render"))
    implementation(project(":lib:docker"))
    implementation(project(":lib:ssh"))
    processor(project.rootProject.ext.jarDependency("org.inferred", "freebuilder"))
    implementation(project.rootProject.ext.jarDependency("com.google.guava", "guava"))
    implementation(project.rootProject.ext.jarDependency("org.apache.commons", "commons-lang3"))
    implementation(project.rootProject.ext.jarDependency("org.freemarker", "freemarker"))
    implementation(project.rootProject.ext.jarDependency("org.apache.commons", "commons-lang3"))
    implementation(project.rootProject.ext.jarDependency("commons-io", "commons-io"))
    implementation("org.springframework.boot:spring-boot-starter-web")

    testImplementation(project.rootProject.ext.jarDependency("org.junit.jupiter", "junit-jupiter-api"))
    testRuntimeOnly(project.rootProject.ext.jarDependency("org.junit.jupiter", "junit-jupiter-engine"))
    testImplementation(project.rootProject.ext.jarDependency("com.github.docker-java", "docker-java-core"))
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
}

test {
    useJUnitPlatform()
    def setSystemPropertyByProjectProperty = { String name, String defaultValue ->
        systemProperty(name, project.getProperties().getOrDefault(name, defaultValue))
    }
    setSystemPropertyByProjectProperty("blackhole.test.master_host", null)
    setSystemPropertyByProjectProperty("blackhole.test.master_port", "22")
    setSystemPropertyByProjectProperty("blackhole.test.worker_host.list", null)
    setSystemPropertyByProjectProperty("blackhole.test.worker_port", "22")
    setSystemPropertyByProjectProperty("blackhole.test.username", "root")
    setSystemPropertyByProjectProperty("blackhole.test.password", "123456")
    setSystemPropertyByProjectProperty(
            "blackhole.army.knife.springboot.package.path",
            bootJar.archiveFile.get().asFile.getAbsolutePath()
    )
    if (!bootJar.archiveFile.get().asFile.exists()) {
        bootJar.archiveFile.get().asFile.createNewFile();
    }
}

task runJar(type: Exec) {
    executable("java")
    args(
            String.format("-Dspring.config.location=%s", project.file("application.yaml")),
            "-jar", project.file(String.format("build/libs/%s-%s.jar", project.getName(), project.getVersion())),
            project.file("template"),
    )
    doFirst {
        println(String.format("running command: %s", String.join(" ", getCommandLine())))
    }
    dependsOn(bootJar)
}