buildscript {
    repositories {
        for (mavenRepositoryUrl in project.rootProject.ext.mavenRepositoryUrlList) {
            maven { url(mavenRepositoryUrl) }
        }
    }
}
group = project.rootProject.ext.constructGroupName("application", project.getName())
version = project.rootProject.constructVersion()

/********************** build docker images **********************/
def buildAbsolutePath = project.buildDir.getAbsolutePath()
def buildDockerPath = "${buildAbsolutePath}/docker"
def httpProxy = java.util.Optional.ofNullable(System.getenv("http_proxy")).orElse("")
def httpsProxy = java.util.Optional.ofNullable(System.getenv("https_proxy")).orElse("")

def keysDirectory = "${buildDockerPath}/keys"
task copyKeys(type: Copy) {
    from(project.rootProject.ext.idRsaFile)
    from(project.rootProject.ext.idRsaPubFile)
    into(keysDirectory)
    dependsOn(":application:tool:generateRsaKeyFiles")
}
def imageName = "${project.rootProject.ext.dockerImageNamePrefix}_${project.getName()}:${project.getVersion()}"
task buildImage(type: Exec) {
    commandLine(
            "docker",
            "build", buildDockerPath,
            "-f", "${project.file("docker/ssh.dockerfile")}",
            "--build-arg", "http_proxy=${httpProxy}",
            "--build-arg", "https_proxy=${httpsProxy}",
            "-t", imageName,
    )
    doFirst {
        println("running command: ${String.join(" ", getCommandLine())}")
    }
    dependsOn(copyKeys)
}
/********************** build docker images ends **********************/
