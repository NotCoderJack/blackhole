buildscript {
    repositories aliyun_repositories
}
plugins {
    id "org.inferred.processors" version "3.3.0"
    id "java"
    id "com.google.protobuf" version "0.8.12"
    id "idea"
}

group = String.format("%s.%s", project.main_group, project.getName())
version = project.main_version
sourceCompatibility = project.java_version
targetCompatibility = project.java_version

repositories.clear()
repositories.addAll(buildscript.repositories)

sourceSets {
    main {
        java {
            srcDirs "build/generated/source/proto/main/grpc"
            srcDirs "build/generated/source/proto/main/java"
            srcDirs "build/generated/sources/annotationProcessor/java/main"
        }
    }
}

def grpcVersion = "1.30.1"
def slf4jVersion = "1.7.15"
def jacksonVersion = "2.9.8"
dependencies {
    implementation(project(":core"))
    processor "org.inferred:freebuilder:2.6.1"
    implementation("com.google.guava:guava:29.0-jre")
    implementation("org.apache.commons:commons-lang3:3.0")
    implementation("org.slf4j:slf4j-api:${slf4jVersion}")
    implementation("org.freemarker:freemarker:2.3.9")
    implementation("org.apache.commons:commons-exec:1.3")
    implementation("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}")
    implementation("com.fasterxml.jackson.core:jackson-core:${jacksonVersion}")
    implementation("com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}")
    // grpc packages: avoid sub-dependencies downloading from repo.maven.apache.org
    implementation("io.grpc:grpc-api:${grpcVersion}")
    implementation("io.grpc:grpc-core:${grpcVersion}")
    implementation("io.grpc:grpc-netty-shaded:${grpcVersion}")
    implementation("io.grpc:grpc-protobuf:${grpcVersion}")
    implementation("io.grpc:grpc-stub:${grpcVersion}")
    implementation("info.picocli:picocli:4.5.0")
    // necessary for Java 9+
    compileOnly("org.apache.tomcat:annotations-api:6.0.53")
    testImplementation("junit:junit:4.12")
    testImplementation("org.slf4j:slf4j-simple:${slf4jVersion}")
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
}
test {
    useJUnit()
}
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.12.0"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.31.0"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}
jar {
    manifest {
        attributes(
                'Implementation-Title': 'blackhole.agent',
                'Implementation-Version': project.getVersion(),
                'Main-Class': 'tech.geekcity.blackhole.agent.EntryPoint'
        )
    }
    setArchivesBaseName("${project.getName()}-${project.getVersion()}-with-dependencies.jar")
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task runJar(type: Exec) {
    executable("java")
    args(
            String.format("-Dspring.config.location=%s", project.file("application.yaml")),
            "-jar", project.file(String.format("build/libs/%s-%s.jar", project.getName(), project.getVersion())),
            project.file("template"),
    )
    doFirst {
        println(String.join(" ", getCommandLine()))
    }
}